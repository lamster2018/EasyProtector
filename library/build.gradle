apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode gitVersionCode()
        versionName gitVersionName()

        ndk {
            abiFilters 'armeabi-v7a','arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

publish {
    userOrg = 'lamster2018' //bintray注册的用户名（不是邮箱）
    groupId = 'com.lahm.library'
    artifactId = 'easy-protector-release' //项目名称
    publishVersion = gitVersionName() //版本号
    desc = 'protect your android app' //项目描述。可空。
    website = 'https://github.com/lamster2018/EasyProtector' //项目地址，一般填github地址。可空。
}

/**
 * 通过git的Commit数来获取version code
 * @return
 */
def static gitVersionCode() {
    def cmd = 'git rev-list HEAD --count'
    10000 + cmd.execute().text.trim().toInteger()
}

/**
 * 通过git上的最近一个tag来获取version name
 * @return
 */
def static gitVersionName() {
    def cmd = 'git describe --tags'
    def tag = cmd.execute().text.trim()
    def arr = tag.split("-")
    def name = arr[0]
    return name
}